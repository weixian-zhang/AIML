// Author: Microsoft Azure
// Display name: View data ingested by completed jobs
// Description: View data ingested size by jobs that are completed.
// Categories: Containers,Workloads,Azure Resources
// Resource types: Kubernetes services
// Solutions: ContainerInsights
// Topic: Costing

//Modify StartTime to customize TimeRange for completedjobs inventory.
let startTime = ago(1h);
//Find all the jobs which are completed
let kpi = KubePodInventory
| where TimeGenerated > startTime
| where _IsBillable == true
| where PodStatus in ("Succeeded", "Failed")
| where ControllerKind == "Job";
//Find the the billable data for the jobs
let containerInventory = ContainerInventory
| where TimeGenerated > startTime
| where _IsBillable == true
| summarize BillableDataMBytes = sum(_BilledSize)/ (1000. * 1000.) by ContainerID;
//Join on both the tables to calculate the billable data 
let containerInventoryMB = containerInventory
| join kpi on $left.ContainerID == $right.ContainerID
| summarize MB=sum(BillableDataMBytes);
let kpiMB = kpi
| summarize MB = sum(_BilledSize)/ (1000. * 1000.);
union
(containerInventoryMB),(kpiMB)
| summarize doneJobsInventoryMB=sum(MB)