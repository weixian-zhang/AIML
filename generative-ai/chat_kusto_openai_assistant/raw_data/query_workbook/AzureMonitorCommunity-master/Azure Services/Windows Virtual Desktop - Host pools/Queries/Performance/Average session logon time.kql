// Author: Microsoft Azure
// Display name: Average session logon time
// Description: Average session logon time by Hostpool.
// Categories: Windows Virtual Desktop
// Resource types: Windows Virtual Desktop - Host pools
// Topic: Performance

// Lists average session logon time by hostpool 
WVDConnections  
| where TimeGenerated > ago(24h)  
| where State == "Started"  
| project CorrelationId , UserName, ConnectionType , StartTime=TimeGenerated, _ResourceId 
| join (WVDConnections
    | where State == "Connected"  
    | project EndTime=TimeGenerated, CorrelationId)  
    on CorrelationId  
| project Duration = EndTime - StartTime, _ResourceId  
| summarize AvgDuration=avg(Duration) by _ResourceId
| extend Multi=split(_ResourceId, "/")  
| project AvgDuration, ResourceGroup=Multi[4], HostPool=Multi[8]  
| sort by AvgDuration desc