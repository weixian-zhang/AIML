// Author: Microsoft Azure
// Display name: Compare two periods for query execution times
// Description: Identify queries that have a latency difference exceeding a threshold.
// Categories: Workloads
// Resource types: Azure Database for PostgreSQL servers
// Topic: Troubleshooting

let queryExecutionPrev24h = AzureDiagnostics
| where ResourceProvider == "MICROSOFT.DBFORPOSTGRESQL"
| where Category == "QueryStoreRuntimeStatistics"
//| where LogicalServerName_s == "your server name" // you can run this query for a specific server | extend timestamp_new = make_datetime(start_time_t)
// change range of baseline period based on your needs
| where timestamp_new >= ago(48h) and timestamp_new < ago(24h)
| summarize prevTimeAvg = avg(todecimal(mean_time_s)), prevTimeMax=max(todecimal(mean_time_s)), prevTimeMinTimeStamp=min(timestamp_new), prevTimeMaxTimeStamp=max(timestamp_new), prevTimeExecutionCount=sum(toint(calls_s)) by query_id_d;
AzureDiagnostics
| where ResourceProvider == "MICROSOFT.DBFORPOSTGRESQL"
| where Category == "QueryStoreRuntimeStatistics"
//| where LogicalServerName_s == "your server name" // you can run this query for a specific server 
| extend timestamp_new = make_datetime(start_time_t)
// change range of current period based on your needs
| where timestamp_new >= ago(24h)
| summarize currentTimeAvg=avg(todecimal(mean_time_s)), max(todecimal(mean_time_s)), min(timestamp_new), max(timestamp_new) , currentTimeExecutionCount = sum(toint(calls_s)) by query_id_d
| join kind=inner
    queryExecutionPrev24h
on query_id_d
| extend latencyDiff = currentTimeAvg - prevTimeAvg
| extend latencyDiffPercent = (((prevTimeAvg-currentTimeAvg)/prevTimeAvg)*100)
| extend executionCountDiff = (((todecimal(prevTimeExecutionCount)-todecimal(currentTimeExecutionCount))/todecimal(prevTimeExecutionCount)))*100
| project query_id_d, latencyDiff, latencyDiffPercent, currentTimeAvg, prevTimeAvg, currentTimeExecutionCount, prevTimeExecutionCount,executionCountDiff
// change your threshold of difference between two periods based on your needs
| where latencyDiffPercent < -10
| order by latencyDiffPercent desc