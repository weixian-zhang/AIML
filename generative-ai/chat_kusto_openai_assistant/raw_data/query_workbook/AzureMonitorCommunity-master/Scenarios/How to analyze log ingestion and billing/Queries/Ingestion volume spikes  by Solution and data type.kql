// Author: Microsoft Azure
// Display name: Ingestion volume spikes - by Solution and data type
// Description: Check for ingestion volume spikes per Solution and data type, in the last 24 hour.
// Categories: monitor
// Resource types: Azure Monitor
// Solutions: LogManagement
// Topic: Usage

let StartTime = ago(24h);
let EndTime = now();
let MinRSquare =0.8; // Tune the sensitivity of the detection sensor
Usage
| where TimeGenerated between (StartTime .. EndTime)
// Create a time series of data volume by solution and data type
| make-series RatioSeries=sum(Quantity/1024) default=0 on TimeGenerated in range(StartTime, EndTime,10m) by Solution, DataType
// Apply a 2-line regression to the time series
| extend (RSquare2, SplitIdx, Variance2, RVariance2, LineFit2) = series_fit_2lines(RatioSeries)
// Find out if our 2-line is trending up or down
| extend (Slope, Interception, RSquare, Variance, RVariance, LineFit) = series_fit_line(LineFit2)
// Check whether the line fit reaches the threshold
| project Solution, DataType, Spike = iff(RSquare2 > MinRSquare and Slope != 0, "Spike detected", "No spike")