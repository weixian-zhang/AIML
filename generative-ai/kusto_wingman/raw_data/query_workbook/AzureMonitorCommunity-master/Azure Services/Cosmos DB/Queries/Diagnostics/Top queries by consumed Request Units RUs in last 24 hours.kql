// Author: Microsoft Azure
// Display name: Top queries by consumed Request Units (RUs) in last 24 hours
// Description: Identify top queries on Cosmos resources by count and RU charge of each query.
// Categories: Azure Resources
// Resource types: Cosmos DB
// Topic: Diagnostics

let queryRUChargeData = AzureDiagnostics
| where Category == "DataPlaneRequests"
| where OperationName == "Query"
| project requestCharge_s, activityId_g, databaseName_s, collectionName_s, requestResourceType_s, requestResourceId_s, OperationName, TimeGenerated, callerId_s, clientIpAddress_s, userAgent_s;
AzureDiagnostics
| where Category == "QueryRuntimeStatistics"
| join queryRUChargeData on $left.activityId_g == $right.activityId_g
| summarize numberOfTimesRun = count(), totalConsumedRU = sum(todouble(requestCharge_s1)) by databaseName_s, collectionName_s, OperationName1, requestResourceType_s1, requestResourceId_s1, querytext_s, callerId_s1, clientIpAddress_s1, userAgent_s1, _ResourceId, bin(TimeGenerated1, 1min) //bin by 1 minute
| extend averageRUPerExecution = totalConsumedRU / numberOfTimesRun
| order by averageRUPerExecution desc