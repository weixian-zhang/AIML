// Author: Microsoft Azure
// Display name: Policies with retention duration modified
// Description: Find out if the retention duration of any policy has been modified in the selected time range.
// Categories: IT & Management Tools,Security
// Resource types: Recovery Services vaults
// Topic: Backup Settings Changes

//Get all Policies by Vault and Retention Duration, at the start of the selected time range.
let PoliciesAtStartOfPeriod = AddonAzureBackupPolicy
| where OperationName == "Policy" 
|  summarize arg_min(TimeGenerated, *) by PolicyUniqueId,ResourceId 
| project PolicyUniqueId, ResourceId, DailyRetentionDuration1=DailyRetentionDuration, WeeklyRetentionDuration1=WeeklyRetentionDuration, MonthlyRetentionDuration1=MonthlyRetentionDuration, YearlyRetentionDuration1=YearlyRetentionDuration;

//Get all Policies by Vault and Retention Duration, at the end of the selected time range
let PoliciesAtEndOfPeriod = AddonAzureBackupPolicy
| where OperationName == "Policy"
|  summarize arg_max(TimeGenerated, *) by PolicyUniqueId,ResourceId
| project PolicyUniqueId, ResourceId, DailyRetentionDuration2=DailyRetentionDuration, WeeklyRetentionDuration2=WeeklyRetentionDuration, MonthlyRetentionDuration2=MonthlyRetentionDuration, YearlyRetentionDuration2=YearlyRetentionDuration;

//Get all Policies for which Daily/Weekly/Monthly/Yearly Retention Duration has been modified in the selected time range
PoliciesAtStartOfPeriod
| join (PoliciesAtEndOfPeriod) on PolicyUniqueId, ResourceId
 | where DailyRetentionDuration1!=DailyRetentionDuration2 or WeeklyRetentionDuration1!=WeeklyRetentionDuration2 or MonthlyRetentionDuration1!=MonthlyRetentionDuration2 or YearlyRetentionDuration1!=YearlyRetentionDuration2