// Author: Microsoft Azure
// Display name: Ingested volume spikes and slopes - by Azure resource
// Description: List the identified ingestion volume spikes and the slope of each spike (positive is upward spike, negative is downward).
// Categories: monitor
// Resource types: Azure Monitor
// Solutions: LogManagement
// Topic: Usage

let StartTime = ago(6h);
let EndTime = now();
let MinRSquare = 0.8; // Tune the sensitivity of the detection sensor
union *
| where TimeGenerated between (StartTime .. EndTime)
// Create a time series of data volume by resource id
| where isempty(_ResourceId) == False
| make-series RatioSeries=sum(_BilledSize) default=0 on TimeGenerated in range(StartTime, EndTime, 10m) by _ResourceId
// Apply a 2-line regression to the time series
| extend (RSquare2, SplitIdx, Variance2, RVariance2, LineFit2) = series_fit_2lines(RatioSeries)
// Find out if our 2-line is trending up or down
| extend (Slope, Interception, RSquare, Variance, RVariance, LineFit) = series_fit_line(LineFit2)
// Check whether the line fit reaches the threshold
| where RSquare2 > MinRSquare and Slope != 0
| project _ResourceId, Slope 